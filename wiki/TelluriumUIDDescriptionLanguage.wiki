#summary Tellurium UID Description Language (UDL).
#labels Phase-Design,Phase-Support
<wiki:toc max_depth="5" />

= Introduction =

[http://code.google.com/p/aost/wiki/UserGuide070TelluriumBasics#UiID_Attribute UI Object ID] (UID) is used to identify and describe a UI object in Tellurium Automated Testing Framework. For example, in the following Google Search Module, the uid attribute is the UID. UID "Input" is the name of the InputBox.

{{{
   ui.Container(uid: "GoogleSearchModule", clocator: [tag: "td"], group: "true"){
     InputBox(uid: "Input", clocator: [title: "Google Search"])
     SubmitButton(uid: "Search", clocator: [name: "btnG", value: "Google Search"])
     SubmitButton(uid: "ImFeelingLucky", clocator: [value: "I'm Feeling Lucky"])
   }
}}}  

Why do we need a language to describe the name of a UI object in Tellurium, then?
The answer is that UID is not just the name of a UI object, it is also used to describe the dynamic factors in a [http://code.google.com/p/aost/wiki/UserGuide070TelluriumBasics#UI_Templates Tellurium UI template].

Tellurium UI templates have two purposes:

  # When there are many identical UI elements, use one template to represent them all.
  # When there are variable/dynamic sizes of UI elements at runtime, the patterns are known, but not the size.

More specifically, Table, StandardTable, and List are the three Tellurium objects that define UI templates. The Table object is a special case of the StandardTable object.

  # *Table* and *StandardTable* define two dimensional UI templates. 
  # *List* defines one dimensional UI templates. 

As a result, the Tellurium UID Description Language (UDL) is used to 
  # address the dynamic factors in Tellurium UI templates
  # increase the flexibility of Tellurium UI templates.

= Tellurium UID Description Language =

Tellurium UID Description Language (UDL) is implemented with [http://www.antlr.org/ the Antlr 3 parser generator]. The implementation details can be found [http://code.google.com/p/aost/wiki/BuildYourOwnJavaParserWithAntlr3 here]. 

We like to focus on the grammars and the use of UDL.

== UDL Grammars ==

The UDL grammars are defined as follows,

{{{
grammar Udl;

uid	
	: 	baseUid
	|	listUid
	|	tableUid	
	;
	
baseUid 
	:	ID
	;
	
listUid
	:	'{' INDEX '}'
	|	'{' INDEX '}' 'as' ID
	;
		
tableUid 
	:	tableHeaderUid
	|	tableFooterUid 
	|	tableBodyUid
	;
	
tableHeaderUid
	:	'{' 'header' ':' INDEX '}'
	|	'{' 'header' ':' INDEX '}' 'as' ID
	;
	
tableFooterUid
	:       '{' 'footer' ':' INDEX '}'
	|       '{' 'footer' ':' INDEX '}' 'as' ID
	;
	
tableBodyUid
        :	'{' 'row' ':' INDEX ',' 'column' ':' INDEX '}'
        |	'{' 'row' ':' INDEX ',' 'column' ':' INDEX '}' 'as' ID
        |	'{' 'row' '->' ID ',' 'column' ':' INDEX '}'
        |	'{' 'row' '->' ID ',' 'column' ':' INDEX '}' 'as' ID
        |	'{' 'row' ':' INDEX ',' 'column' '->' ID '}'
        |	'{' 'row' ':' INDEX ',' 'column' '->' ID '}' 'as' ID
        |	'{' 'row' '->' ID ',' 'column' '->' ID '}'
        |	'{' 'row' '->' ID ',' 'column' '->' ID '}' 'as' ID
        |       '{' 'tbody' ':' INDEX ',' 'row' ':' INDEX ',' 'column' ':' INDEX '}'
        |       '{' 'tbody' ':' INDEX ',' 'row' ':' INDEX ',' 'column' ':' INDEX '}' 'as' ID
        |	'{' 'tbody' ':' INDEX ',' 'row' '->' ID ',' 'column' ':' INDEX '}'
        |	'{' 'tbody' ':' INDEX ',' 'row' '->' ID ',' 'column' ':' INDEX '}' 'as' ID
        |       '{' 'tbody' ':' INDEX ',' 'row' ':' INDEX ',' 'column' '->' ID '}'
        |       '{' 'tbody' ':' INDEX ',' 'row' ':' INDEX ',' 'column' '->' ID '}' 'as' ID
        |	'{' 'tbody' ':' INDEX ',' 'row' '->' ID ',' 'column' '->' ID '}'
        |	'{' 'tbody' ':' INDEX ',' 'row' '->' ID ',' 'column' '->' ID '}' 'as' ID
        ;              			
							
fragment LETTER : ('a'..'z' | 'A'..'Z') ;
fragment DIGIT : '0'..'9';
INDEX	:	(DIGIT+ |'all' | 'odd' | 'even' | 'any' | 'first' | 'last' );   
ID 	: 	LETTER (LETTER | DIGIT)*;
WS 	: 	(' ' | '\t' | '\n' | '\r' | '\f')+ {$channel = HIDDEN;};
}}}

The grammars defined the UIDs for the following three categories of [http://code.google.com/p/aost/wiki/UserGuide070UIObjects Tellurium UI Objects].
  # Regular UI objects without UI templates such as [http://code.google.com/p/aost/wiki/UserGuide070UIObjects#Input_Box Input Box] and [http://code.google.com/p/aost/wiki/UserGuide070UIObjects#Container Container]
  # List type UI object, i.e., [http://code.google.com/p/aost/wiki/UserGuide070UIObjects#List List]
  # Table type UI objects including [http://code.google.com/p/aost/wiki/UserGuide070UIObjects#Table Table] and [http://code.google.com/p/aost/wiki/UserGuide070UIObjects#Standard_Table StandardTable]. 

We like to go over the grammars in details.

=== ID ===

ID is the name of the UI object. The ID in the UDL starts with a letter and is followed by digits, letters, and "`_`" as follows.
 

http://tellurium-users.googlegroups.com/web/udl_id.jpg?gda=qMY0wDwAAAA7fMi2EBxrNTLhqoq3FzPrYxkpUpFnzJyAbOi2fmAIqphN994tpHQvlLtY7uJzmPP9Wm-ajmzVoAFUlE7c_fAt&gsc=6cd9zQsAAAAkIbFAyUaYDIw1XZeMnNrq

=== Index ===

Index defines the position of the UI object. The index in the UDL can be any of the following values:

  * Number, for example, "5".
  * "first", the first element.
  * "last", the last element.
  * "any", any position, usually the position is dynamic at runtime.
  * "odd", the odd elements.
  * "even", the even elements
  * "all", wild match if not exact matches found.

 http://tellurium-users.googlegroups.com/web/udl_index.jpg?gda=IiTkhD8AAAA7fMi2EBxrNTLhqoq3FzPrmWBudP5qiPQkac1peLSYWg56keJ9cuAKmA_YKbtH6VuccyFKn-rNKC-d1pM_IdV0&gsc=myTuAgsAAABNyAlnqKoVxICsBHUflUOz

=== Regular UI Objects ===

For most [http://code.google.com/p/aost/wiki/UserGuide070UIObjects Tellurium UI objects] without UI templates, the UID is an ID, i.e., a name. They don't need any index description since the name and the UI object is one to one mapping.

http://tellurium-users.googlegroups.com/web/udl_baseuid.jpg?gda=i6sK9EEAAAA7fMi2EBxrNTLhqoq3FzPrmWBudP5qiPQkac1peLSYWuuh6eJkplv9T-qV3sY7HytTCT_pCLcFTwcI3Sro5jAzlXFeCn-cdYleF-vtiGpWAA&gsc=myTuAgsAAABNyAlnqKoVxICsBHUflUOz

For example, the UID of the container is "GoogleSearchModule" and "Search" is the name of one SubmitButton in the previous Google search module.

=== List ===

[http://code.google.com/p/aost/wiki/UserGuide070UIObjects#List The List object] defines an array of UI objects and its UID consists of an index and an optional name.


http://tellurium-users.googlegroups.com/web/udl_list.jpg?gda=lwZHAT4AAAA7fMi2EBxrNTLhqoq3FzPrmWBudP5qiPQkac1peLSYWjrjWAqsK6h2ox84bV_5MMfjsKXVs-X7bdXZc5buSfmx&gsc=myTuAgsAAABNyAlnqKoVxICsBHUflUOz

For example, the following List "A", defines the following Ui Objects:

  * InputBox, position at "1" and name "Input".
  * Selector, position at "2" and name "Select".
  * TextBox, represents the rest objects not at position "1" and "2".

{{{
   ui.List(uid: "A", clocator: [tag: "table"], separator: "tr") {
      InputBox(uid: "{1} as Input", clocator: [:])
      Selector(uid: "{2} as Select", clocator: [:])
      TextBox(uid: "{all}", clocator: [tag: "div"])
   }
}}}

We can use {{{"A[1]"}}} or {{{"A.Input"}}} to reference the InputBox object. {{{"A[3]"}}} and {{{"A[6]"}}} are mapped to the TextBox object.

=== Table ===

As we said, the [http://code.google.com/p/aost/wiki/UserGuide070UIObjects#Table Table object] is a special case of the more general object [http://code.google.com/p/aost/wiki/UserGuide070UIObjects#Standard_Table StandardTable], which includes a header, a footer, and one or multiple body sections.

==== Header ====

Table header is very much similar to the List, but its index starts with the "header" indicator.

http://tellurium-users.googlegroups.com/web/udl_tableheader.jpg?gda=Ido_lkUAAAA7fMi2EBxrNTLhqoq3FzPrmWBudP5qiPQkac1peLSYWlkJCT0fa5iWqwJzQkXxz1JzlqnWZQD3y6jZqCMfSFQ6Gu1iLHeqhw4ZZRj3RjJ_-A&gsc=myTuAgsAAABNyAlnqKoVxICsBHUflUOz

==== Footer ====

The footer is similar to header and its index starts with the "footer" indicator.

http://tellurium-users.googlegroups.com/web/udl_tablefooter.jpg?gda=P8Ka3UUAAAA7fMi2EBxrNTLhqoq3FzPrmWBudP5qiPQkac1peLSYWm264Vj_bu4ncefYGcb6mYpzlqnWZQD3y6jZqCMfSFQ6Gu1iLHeqhw4ZZRj3RjJ_-A&gsc=myTuAgsAAABNyAlnqKoVxICsBHUflUOz

==== Body ====

The body is more complicated since it is represented by the triple {{{[tbody, row, column]}}}.

http://tellurium-users.googlegroups.com/web/udl_tablebody.jpg?gda=iMD3okMAAAA7fMi2EBxrNTLhqoq3FzPrmWBudP5qiPQkac1peLSYWuiuCXJQSXdF4sM7I0Qg4aMytiJ-HdGYYcPi_09pl8N7FWLveOaWjzbYnpnkpmxcWg&gsc=myTuAgsAAABNyAlnqKoVxICsBHUflUOz

The tbody can be omitted if we have only one tbody. Thus, we can reduce the above subrules to 8. The subrules look complicated, but they are not. Each row and column can be either an index just like that in the List object or a reference to the ID of a header or a footer. The reference is defined by the {{{->}}} symbol. In this way, the row or column position can be dynamic and follow a header or a footer UI object.

