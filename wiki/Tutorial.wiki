#labels Featured,Phase-Implementation
= Tutorial for Tellurium =

== How to use Tellurium ==

There are two ways to use Tellurium. The first one is first write your UI modules
in different Groovy files, which extends the DslContext class. Then write 
JUnit test cases. You can also include them in a test suite. 

Starting from Tellurium 0.4.0, JUnit 4.4 is supported. Usually, users only need to extend TelluriumJavaTestCase to write Tellurium Tests. The TelluriumJavaTestCase has already included the setUp and tearDown methods in class level using JUnit 4 @BeforeClass and
@AfterClass annotations. If users want to include multiple test cases in one test suite, they should use BaseTelluriumJavaTestCase so that all the test cases can share the same Selenium connection. If users really want to write Tellurium tests in groovy, please use TelluriumGroovyTestCase and TelluriumSuiteGroovyTestCase instead.

The other way is to write DSL script directly, which is recommended only for non-developers since you cannot create different modules if write in pure DSL. 
The DslScriptExecutor can be used to run the .dsl files.

== Examples ==

=== Google Start Page ===

First, create the google start page module in Groovy
{{{
class NewGoogleStartPage extends DslContext{
    public void defineUi() {
        ui.Container(uid: "google_start_page", clocator: [tag: "td"], group: "true"){
            InputBox(uid: "searchbox", clocator: [title: "Google Search"])
            SubmitButton(uid: "googlesearch", clocator: [name: "btnG", value: "Google Search"])
            SubmitButton(uid: "Imfeelinglucky", clocator: [value: "I'm Feeling Lucky"])
        }
    }

    def doGoogleSearch(String input){
        type "searchbox", input
        pause 500
        click "googlesearch"
        waitForPageToLoad 30000
    }

    def doImFeelingLucky(String input){
        type "searchbox", input
        pause 500
        click "Imfeelinglucky"
        waitForPageToLoad 30000
    }
}
}}}

Then, write the JUnit test case as follows,

{{{
public class GoogleStartPageJavaTestCase extends TelluriumJavaTestCase {

    protected static NewGoogleStartPage ngsp;

    @BeforeClass
    public static void initUi() {
        ngsp = new NewGoogleStartPage();
        ngsp.defineUi();
    }

    @Test
    public void testGoogleSearch(){
        connectUrl("http://www.google.com");
        ngsp.doGoogleSearch("tellurium selenium Groovy Test");
   }

   @Test
   public void testGoogleSearchFeelingLucky(){
        connectUrl("http://www.google.com");
        ngsp.doImFeelingLucky("tellurium selenium DSL Testing");
   }
}

}}}

Optionally, you can also write the DSL script file directly 

{{{
ui.Container(uid: "google_start_page", clocator: [tag: "td"], group: "true"){
    InputBox(uid: "searchbox", clocator: [title: "Google Search"])
    SubmitButton(uid: "googlesearch", clocator: [name: "btnG", value: "Google Search"])
    SubmitButton(uid: "Imfeelinglucky", clocator: [value: "I'm Feeling Lucky"])
}

openUrl "http://www.google.com"
type "google_start_page.searchbox", "Tellurium Selenium test"
pause 500
click "google_start_page.Imfeelinglucky"
waitForPageToLoad 30000
}}}

Then use DslScriptExecutor to run it.

=== Google Code Hosting Page ===

The google code hosting page includes nested tables to show labels for 
projects. Tellurium supports nested IDs. For example XXX.YYY.ZZZ represents the fact that
the UI object YYY is inside XXX and it holds the ZZZ Ui object. Usually 
the id of the UI object is the one you specified in the ui.Container block.
The exceptions are tables and lists, which use {{{[x][y] or [x]}}} to reference
its elements inside.

The Ui module for the label tables is defined as

{{{
ui.Table(uid: "labels_table", clocator: [:], group: "true"){
    //Here we defined templates for the elements of the table
    //id: "row: 1, column: 1" means the TextBox is a template for
    //row 1, column 1.
    TextBox(uid: "row: 1, column: 1", clocator: [tag: "div", text: "Example project labels:"])
    //inner table, which is a template for (row 2, column 1) of the outer table
    Table(uid: "row: 2, column: 1", clocator: [header: "/div[@id=\"popular\"]"]){
        //"all" means this is the template for all table elements
        UrlLink(uid: "all", locator: "/a")
   }
}

}}}

The UI objects defined inside a table are actually templates for table elements.
Note the ids of the templates must follow the name convention:

  # Template for (i row, j column), the id should be "row: i, column: j"
  # The wild case (i.e., match all) for row or column is {{{"*"}}}, or you do not specify the row or column. For example, {{{"row : *, column: 3"}}} means template for (the 3rd column, all rows). "column: 3" means the same thing


  # "all" is used for matching all rows and columns

If no template is defined for a (row, column), it is assumed to be a TextBox by default.

When you look for an element's (row i, column j) Object-type in run-time, the following
rules apply:

  # First, the system will try to find the template defined for (row i, column j),return it if found.
  # Otherwise, the system will try the template for (all rows, column j), return it if found.
  # If not, the system will try the template for (row i, all columns), return it if found.
  # If still cannot find, the system will try the template for "all", return it if found.
  # Last, if no template could be found. Return the TextBox as the default template.
  
The module functions are defined as follows,

{{{

    public String getModuleLabel(){
        getText "labels_table[1][1]"
    }

    public int getLabelTableRowNum(){
        getTableMaxRowNum "labels_table[2][1]"
    }

    public int getLabelTableColumnNum(){
        getTableMaxColumnNum "labels_table[2][1]"
    }

    def getTableElement(int row, int column){
        getUiElement "labels_table[2][1].[${row}][${column}]"
    }

    Map getAllLabels(){
        int nrow = getTableMaxRowNum("labels_table[2][1]")
        int ncolumn = getTableMaxColumnNum("labels_table[2][1]")

        def map = [:]
        for(int i=1; i<=nrow; i++){
            for(int j=1; j<=ncolumn; j++){
                String label = getText("labels_table[2][1].[${i}][${j}]")
                map.put(label, [i, j])
            }
        }

        return map
    }

    void clickOnLable(int row, int column){
        click  "labels_table[2][1].[${row}][${column}]"
        waitForPageToLoad 30000 
    }

    String getUrlLink(int row, int column){

        getLink("labels_table[2][1].[${row}][${column}]")
    }

}}}

The actual test case is 

{{{

  public class GoogleCodeHostingJavaTestCase extends TelluriumJavaTestCase {
    private static NewGoogleCodeHosting ngch;


    @BeforeClass
    public static void initUi() {
        ngch = new NewGoogleCodeHosting();
        ngch.defineUi();
    }

    @AfterClass
    public static void setUpAfterClass(){

    }
    
    @Before
    public void setUpBeforeTest(){
        connectUrl("http://code.google.com/hosting/");
    }

    @After
    public void tearDownAfterTest(){

    }

    @Test
    public void testCodeLabelTable(){
        String label = ngch.getModuleLabel();
        assertEquals("Example project labels:", label);

        int nrow = ngch.getLabelTableRowNum();
        assertEquals(3, nrow);

        int ncolumn = ngch.getLabelTableColumnNum();
        assertEquals(6, ncolumn);

        for(int i=1; i<=nrow; i++){
            for(int j=1; j<=ncolumn; j++){
               Object obj = ngch.getTableElement(i, j);
               assertTrue(obj instanceof UrlLink);
            }
        }
    }

    @Test
    public void testClickOnLabel(){
        Map map = ngch.getAllLabels();
        assertEquals(18, map.size());

        List index = (List) map.get("Java");
        assertNotNull(index);
        assertEquals(2, index.size());

        //find the url link
        int first = (Integer)index.get(0);
        int second = (Integer)index.get(1);
        String url = ngch.getUrlLink(first, second);
        assertEquals("search?q=label%3aJava", url);

        //click on "Java" link
        ngch.clickOnLable(first, second);

    }
}}}

=== Google Book List Page ===

The Google Book List test case is used to demonstrate the List UI object. 
The List UI object likes the Table, but it is one dimension. For example, you 
need to use {{{list1[2]}}} format to represent the UI object id inside the list.
Also, the List comes with an option "separator", which is used to indicate the delimiter for different elements in the list.

The portion of book category "Fiction" is chosen as our demo, which is defined as:
{{{
ui.Container(uid: "GoogleBooksList", clocator: [tag: "table", id: "hp_table"], group: "true"){
   TextBox(uid: "category", clocator: [tag: "div", class: "sub_cat_title"])
   List(uid: "subcategory", clocator: [tag: "div", class:"sub_cat_section"], separator: "/p"){
      UrlLink(uid: "all", locator: "/a")
   }
}

}}}

Similar to the Table example, the UrlLink inside the List "subcategory" defines a template
for elements in the List. ID "all" means it is for all elements. Otherwise, you should specify the index of the element that template applies for. When the system tries to find the UI object for {{{List[x]}}} in runtime, it first checks if there is a template for index {{{x}}}, if not, it then tries the "all" template. If no template found, it returns a TextBox as the default object.

The actions in the module is defined as
{{{
    String getCategory(){
        getText "GoogleBooksList.category"
    }

    int getListSize(){
        getListSize "GoogleBooksList.subcategory"
    }

    def getAllObjectInList(){
        int size = getListSize()
        List list = new ArrayList()
        for(int i=1; i<=size; i++){
           list.add(getUiElement("GoogleBooksList.subcategory[${i}]"))
        }

        return list
    }

    def clickList(int index){
        click "GoogleBooksList.subcategory[${index}]"
        pause 5000
    }

    String getText(int index){
        getText "GoogleBooksList.subcategory[${index}]"
    }

}}}

And the test case is as follows, 

{{{
public class GoogleBooksListJavaTestCase extends TelluriumJavaTestCase {
    private static NewGoogleBooksList ngbl;

    @BeforeClass
    public static void initUi() {
        ngbl = new NewGoogleBooksList();
        ngbl.defineUi();
    }

    @Test
    public void testBookCategory(){
        connectUrl("http://books.google.com/");
        String category = ngbl.getCategory();
        assertEquals("Fiction", category);

        int size = ngbl.getListSize();

        assertEquals(8, size);           
    }
}

}}}  

== Advanced Examples ==

Tellurium includes advance examples to use Tellurium project website as a reference and implement test cases for different pages. They are non-trivial and rather complicated Tellurium tests. 

=== Tellurium Project Home Page ===

To demonstrate how to define multiple UI objects, the Tellurium project home page is described as:

{{{
//define the menu
//It is fine to use Container to abstract Table if you have special table
ui.Container(uid: "menu", clocator: [tag: "table", id: "mt", trailer: "/tbody/tr/th"], group: "true"){
//since the actual text is  Project&nbsp;Home, we can useString partial match here. Note "%%" stands for partial match
    UrlLink(uid: "project_home", clocator: [text: "%%Home"])
    UrlLink(uid: "downloads", clocator: [text: "Downloads"])
    UrlLink(uid: "wiki", clocator: [text: "Wiki"])
    UrlLink(uid: "issues", clocator: [text: "Issues"])
    UrlLink(uid: "source", clocator: [text: "Source"])
}

//define the search module, which includes an input box, two search buttons
ui.Form(uid: "search", clocator: [:], group: "true"){
    InputBox(uid: "searchbox", clocator: [name: "q"])
    SubmitButton(uid: "search_project_button", clocator: [value: "Search Projects"])
    SubmitButton(uid: "search_web_button", clocator: [value: "Search the Web"])
}
}}}

=== Tellurium Project Download Page ===

The download page can be modularized as follows,

{{{
//define UI module of a form include download type selector and download search
ui.Form(uid: "downloadSearch", clocator: [action: "list", method: "get"], group: "true") {
    Selector(uid: "downloadType", clocator: [name: "can", id: "can"])
    TextBox(uid: "searchLabel", clocator: [tag: "span"])
    InputBox(uid: "searchBox", clocator: [name: "q"])
    SubmitButton(uid: "searchButton", clocator: [value: "Search"])
}

ui.Table(uid: "downloadResult", clocator: [id: "resultstable", class: "results"], group: "true"){
    //define table header
    //for the border column
    TextBox(uid: "header: 1", clocator: [:])
    UrlLink(uid: "header: 2", clocator: [text: "%%Filename"])
    UrlLink(uid: "header: 3", clocator: [text: "%%Summary + Labels"])
    UrlLink(uid: "header: 4", clocator: [text: "%%Uploaded"])
    UrlLink(uid: "header: 5", clocator: [text: "%%Size"])
    UrlLink(uid: "header: 6", clocator: [text: "%%DownloadCount"])
    UrlLink(uid: "header: 7", clocator: [text: "%%..."])

    //define table elements
    //for the border column
    TextBox(uid: "row: *, column: 1", clocator: [:])
    //the summary + labels column consists of a list of UrlLinks
    List(uid: "row:*, column: 3", clocator: [:]){
        UrlLink(uid: "all", clocator: [:])
    }
    //For the rest, just UrlLink
    UrlLink(uid: "all", clocator: [:])
}
}}}

The form includes a download type selector and a download search button. The table represents the data grid listing the download file names, summary + labels, upload date, size, and download count.

=== Tellurium Project WiKi Page === 

Similarly, the wiki page is illustrated as follows,

{{{
//define UI module of a form include wiki page type selector and page search
ui.Form(uid: "wikiSearch", clocator: [action: "/p/aost/w/list", method: "get"], group: "true") {
    Selector(uid: "pageType", clocator: [name: "can", id: "can"])
    TextBox(uid: "searchLabel", clocator: [tag: "span"])
    InputBox(uid: "searchBox", clocator: [name: "q"])
    SubmitButton(uid: "searchButton", clocator: [value: "Search"])
}

ui.Table(uid: "wikiResult", clocator: [id: "resultstable", class: "results"], group: "true"){
    //define table header
    //for the border column
    TextBox(uid: "header: 1", clocator: [:])
    UrlLink(uid: "header: 2", clocator: [text: "%%PageName"])
    UrlLink(uid: "header: 3", clocator: [text: "%%Summary + Labels"])
    UrlLink(uid: "header: 4", clocator: [text: "%%Changed"])
    UrlLink(uid: "header: 5", clocator: [text: "%%ChangedBy"])           
    UrlLink(uid: "header: 6", clocator: [text: "%%..."])

    //define table elements
    //for the border column
    TextBox(uid: "row: *, column: 1", clocator: [:])
    //the summary + labels column consists of a list of UrlLinks
    List(uid: "row:*, column: 3", clocator: [:]){
        UrlLink(uid: "all", clocator: [:])
    }
    //For the rest, just UrlLink
    UrlLink(uid: "all", clocator: [:])
}
}}}

=== Tellurium Project Issues Page === 

The Issues page is more complicated and it includes the following Ui modules

{{{
//define UI module of a form include issue type selector and issue search
ui.Form(uid: "issueSearch", clocator: [action: "list", method: "get"], group: "true") {
    Selector(uid: "issueType", clocator: [name: "can", id: "can"])
    TextBox(uid: "searchLabel", clocator: [tag: "span"])
    InputBox(uid: "searchBox", clocator: [name: "q"])
    SubmitButton(uid: "searchButton", clocator: [value: "Search"])
}

ui.Form(uid: "issueAdvancedSearch", clocator: [action: "advsearch.do", method: "post"], group: "true"){
    Table(uid: "searchTable", clocator: [class: "advquery"]){
        Selector(uid: "row:1, column: 3", clocator: [name: "can"])
        SubmitButton(uid: "row:1, column:4", clocator: [value: "Search", name: "btn"])
        InputBox(uid: "row:2, column:3", clocator:[name: "words"])
        InputBox(uid: "row:3, column:3", clocator:[name: "without"])
        InputBox(uid: "row:5, column:3", clocator:[name: "labels"])
        Table(uid: "row:6, column:1", clocator:[:]){
            UrlLink(uid: "row:1, column:1", clocator:[text: "%%More Search Tips"])
        }
        InputBox(uid: "row:6, column:3", clocator:[name: "statuses"])
        InputBox(uid: "row:7, column:2", clocator:[name: "reporters"])
        InputBox(uid: "row:8, column:2", clocator:[name: "owners"])
        InputBox(uid: "row:9, column:2", clocator:[name: "cc"])
        InputBox(uid: "row:10, column:3", clocator:[name: "commentby"])
   }
}

ui.Table(uid: "issueResult", clocator: [id: "resultstable", class: "results"], group: "true") {
    //define table header
    //for the border column
    TextBox(uid: "header: 1",  clocator: [:])
    UrlLink(uid: "header: 2",  clocator: [text: "%%ID"])
    UrlLink(uid: "header: 3",  clocator: [text: "%%Type"])
    UrlLink(uid: "header: 4",  clocator: [text: "%%Status"])
    UrlLink(uid: "header: 5",  clocator: [text: "%%Priority"])
    UrlLink(uid: "header: 6",  clocator: [text: "%%Milestone"])
    UrlLink(uid: "header: 7",  clocator: [text: "%%Owner"])
    UrlLink(uid: "header: 9",  clocator: [text: "%%Summary + Labels"])
    UrlLink(uid: "header: 10", clocator: [text: "%%..."])

    //define table elements
    //for the border column
    TextBox(uid: "row: *, column: 1", clocator: [:])
    //For the rest, just UrlLink
    UrlLink(uid: "all", clocator: [:])
}

//items is a map in the format of "alias name" : menu_item
ui.SimpleMenu(uid: "IdMenu", clocator:[class: "popup", id: "pop_0"],
    items: ["SortUp":"Sort Up", "SortDown":"Sort Down", "HideColumn":"Hide Column"])

//define the dot menu where you can select different column to display
ui.SelectMenu(uid: "selectColumnMenu", clocator:[class: "popup",id: "pop__dot"], title: "Show columns:",
    items: ["ID":"ID", "Type":"Type", "Status":"Status", "Priority":"Priority", "Milestone":"Milestone", "Owner":"Owner", "Summary":"Summary", "Stars":"Stars", "Opened":"Opened", "Closed":"Closed", "Modified":"Modified", "EditColumn":"Edit Column Spec..." ])

//The selector to choose the data grid layout as List or Grid
ui.Option(uid: "layoutSelector"){
    Container(uid: "layoutSelector", clocator: [tag: "div"], group: "true") {
        TextBox(uid: "List", clocator: [tag: "b", text: "List", direct: "true"])
        UrlLink(uid: "Grid", clocator: [text: "Grid", direct: "true"])
    }
    Container(uid: "layoutSelector", clocator: [tag: "div"], group: "true") {
        UrlLink(uid: "List", clocator: [text: "List", direct: "true"])
        TextBox(uid: "Grid", clocator: [tag: "b", text: "Grid", direct: "true"])
    }
}
}}}

Apart from the issue search form, issue advanced search, and the data grid, this page also includes a menu, and a SelectMenu, which is dynamic at runtime. That is to say, the Ui XPath is not fixed at runtime. The Ui Object SelectMenu will check the current DOM to determine what the actual Ui is. The last one is the Option object, which is an abstract object and it holds multiple UIs with each representing a possible UI pattern. The option object will automatically detect which UI pattern you need to use at runtime.  

Please see TelluriumProjectPage, TelluriumDownloadsPage, TelluriumWikiPage, and TelluriumIssuesPage for Ui Modules. See TelluriumProjectPageJavaTestCase, TelluriumDownloadsPageJavaTestCase, TelluriumWikiPageJavaTestCase, and TelluriumIssuesPageJaveTestCase for detailed tests.


== Data Driven Testing Examples ==

To demonstrate data driven testing, we take GoogleBookList and GoogleCodeHosting as examples. First, we create two test modules, i.e., the GoogleBookListModule

{{{
class GoogleBookListModule extends TelluriumDataDrivenModule{

    public void defineModule() {
        //define UI module
        ui.Container(uid: "GoogleBooksList", clocator: [tag: "table", id: "hp_table"], group: "true") {
            TextBox(uid: "category", clocator: [tag: "div", class: "sub_cat_title"])
            List(uid: "subcategory", clocator: [tag: "div", class: "sub_cat_section"], separator: "/p") {
                UrlLink(uid: "all", locator: "/a")
            }
        }

        //define file data format
        fs.FieldSet(name: "GoogleBookList", description: "Google Book List") {
            Test(value: "checkBookList")
            Field(name: "category", description: "book category")
            Field(name: "size", type: "int", description: "google book list size ")
        }

        defineTest("checkBookList"){
            def expectedCategory = bind("GoogleBookList.category")
            def expectedSize = bind("GoogleBookList.size")
            openUrl("http://books.google.com/")
            String category = getText("GoogleBooksList.category")
            compareResult(expectedCategory, category)

            int size = getListSize("GoogleBooksList.subcategory")
            //customize the compareResult and override its behaviour 
            compareResult(expectedSize, size){
                assertTrue(expectedSize == size)
            }
        }
    }
}
}}}

and the GoogleCodeHostingModule

{{{
class GoogleCodeHostingModule extends TelluriumDataDrivenModule{

    public void defineModule() {
       ui.Table(uid: "labels_table", clocator: [:], group: "true"){
         TextBox(uid: "row: 1, column: 1", clocator: [tag: "div", text: "Example project labels:"])
         Table(uid: "row: 2, column: 1", clocator: [header: "/div[@id=\"popular\"]"]){
             UrlLink(uid: "all", locator: "/a")
           }
       }

        //define file data format
        fs.FieldSet(name: "GCHStatus", description: "Google Code Hosting input") {
            Test(value: "getGCHStatus")
            Field(name: "label")
            Field(name: "rowNum", type: "int")
            Field(name: "columNum", type: "int")
        }

        fs.FieldSet(name: "GCHLabel", description: "Google Code Hosting input") {
            Test(value: "clickGCHLabel")
            Field(name: "row", type: "int")
            Field(name: "column", type: "int")
        }

        defineTest("getGCHStatus"){
            def expectedLabel = bind("GCHStatus.label")
            def expectedRowNum = bind("GCHStatus.rowNum")
            def expectedColumnNum = bind("GCHStatus.columNum")

            openUrl("http://code.google.com/hosting/")
            def label = getText("labels_table[1][1]")
            def rownum = getTableMaxRowNum("labels_table[2][1]")
            def columnum = getTableMaxColumnNum("labels_table[2][1]")

            compareResult(expectedLabel, label)
            compareResult(expectedRowNum, rownum)
            compareResult(expectedColumnNum, columnum)
            pause 1000
       }

       defineTest("clickGCHLabel"){
           def row = bind("GCHLabel.row")
           def column = bind("GCHLabel.column")
           
           openUrl("http://code.google.com/hosting/")
           click  "labels_table[2][1].[${row}][${column}]"

           waitForPageToLoad 30000
       }
    }
}
}}}

as shown above, each module defines its own Ui modules, FieldSets (i.e., input data format), and tests. Then, we need to define the actual testing class GoogleBookListCodeHostTest,

{{{
class GoogleBookListCodeHostTest extends TelluriumDataDrivenTest{

    public void testDataDriven() {

        includeModule  example.test.ddt.GoogleBookListModule.class
        includeModule  example.test.ddt.GoogleCodeHostingModule.class

        //load file
        loadData "src/test/example/test/ddt/GoogleBookListCodeHostInput.txt"
        
        //read each line and run the test script until the end of the file
        stepToEnd()

        //close file
        closeData()

    }
}
}}}

This test class simply load the GoogleBookListModule and the GoogleCodeHostingModule, then load the input data from file "src/test/example/test/ddt/GoogleBookListCodeHostInput.txt". After that, it uses "stepToEnd" to drive the testing until reach the end of the file. Finally, it use "closeData" to close the input data stream and output the test results in XML format in console.

The final piece is the input file, which is defined as follows,

{{{
##TEST should be always be the first column

##Data for test "checkBookList"
##TEST | CATEGORY | SIZE
checkBookList|Fiction|8
checkBookList|Fiction|3

##Data for test "getGCHStatus"
##TEST | LABEL | Row Number | Column Number
getGCHStatus |Example project labels:| 3 | 6
getGCHStatus |Example project| 3 | 6

##Data for test "clickGCHLabel"
##TEST | row | column
clickGCHLabel | 1 | 1
clickGCHLabel | 2 | 2
clickGCHLabel | 3 | 3
}}}

When all the above are done, just run the GoogleBookListCodeHostTest class as a regular JUnit test and it will run all the tests for you. 

The test results are shown as follows (Here the time unit is second),

{{{
<TestResults>
  <Total>7</Total>
  <Succeeded>5</Succeeded>
  <Failed>2</Failed>
  <Test name='checkBookList'>
    <Step>1</Step>
    <Passed>true</Passed>
    <Input>
      <test>checkBookList</test>
      <category>Fiction</category>
      <size>8</size>
    </Input>
    <Assertion Expected='Fiction' Actual='Fiction' Passed='true' />
    <Assertion Expected='8' Actual='8' Passed='true' />
    <Status>PROCEEDED</Status>
    <Runtime>5.602938</Runtime>
  </Test>
  <Test name='checkBookList'>
    <Step>2</Step>
    <Passed>false</Passed>
    <Input>
      <test>checkBookList</test>
      <category>Fiction</category>
      <size>3</size>
    </Input>
    <Assertion Expected='Fiction' Actual='Fiction' Passed='true' />
    <Assertion Expected='3' Actual='8' Passed='false' Error='' />
    <Status>PROCEEDED</Status>
    <Runtime>1.653998</Runtime>
  </Test>
  <Test name='getGCHStatus'>
    <Step>3</Step>
    <Passed>true</Passed>
    <Input>
      <test>getGCHStatus</test>
      <label>Example project labels:</label>
      <rowNum>3</rowNum>
      <columNum>6</columNum>
    </Input>
    <Assertion Expected='Example project labels:' Actual='Example project labels:' Passed='true' />
    <Assertion Expected='3' Actual='3' Passed='true' />
    <Assertion Expected='6' Actual='6' Passed='true' />
    <Status>PROCEEDED</Status>
    <Runtime>3.163041</Runtime>
  </Test>
  <Test name='getGCHStatus'>
    <Step>4</Step>
    <Passed>false</Passed>
    <Input>
      <test>getGCHStatus</test>
      <label>Example project</label>
      <rowNum>3</rowNum>
      <columNum>6</columNum>
    </Input>
    <Assertion Expected='Example project' Actual='Example project labels:' Passed='false' Error='expected:&lt;...&gt; but was:&lt;... labels:&gt;' />
    <Assertion Expected='3' Actual='3' Passed='true' />
    <Assertion Expected='6' Actual='6' Passed='true' />
    <Status>PROCEEDED</Status>
    <Runtime>2.115711</Runtime>
  </Test>
  <Test name='clickGCHLabel'>
    <Step>5</Step>
    <Passed>true</Passed>
    <Input>
      <test>clickGCHLabel</test>
      <row>1</row>
      <column>1</column>
    </Input>
    <Status>PROCEEDED</Status>
    <Runtime>2.664228</Runtime>
  </Test>
  <Test name='clickGCHLabel'>
    <Step>6</Step>
    <Passed>true</Passed>
    <Input>
      <test>clickGCHLabel</test>
      <row>2</row>
      <column>2</column>
    </Input>
    <Status>PROCEEDED</Status>
    <Runtime>3.277487</Runtime>
  </Test>
  <Test name='clickGCHLabel'>
    <Step>7</Step>
    <Passed>true</Passed>
    <Input>
      <test>clickGCHLabel</test>
      <row>3</row>
      <column>3</column>
    </Input>
    <Status>PROCEEDED</Status>
    <Runtime>0.985857</Runtime>
  </Test>
</TestResults>

}}}


== DSL Examples ==

Tellurium supports pure dsl tests. The following examples demonstrate how to write pure dsl files as selenium tests.

=== Google Start Page and Tellurium Project Home ===

We created an example dsl test file in src/example/dsl/google.dsl. In this file we define multiple UI modules. Since we have multiple UI modules, we must use the full path, i.e., the top level UI names such as "google_start_page", "menu", and "search" cannot be omitted. 

In test script, you can use any methods defined in DslContext and DslTelluriumGroovyTestCase including assertions, any Groovy code for flow control, or Java code since Groovy is compatible with Java syntax, which is really powerful.

The google.dsl is defined as follows,

{{{
//define google start page
ui.Container(uid: "google_start_page", clocator: [tag: "td"], group: "true"){
    InputBox(uid: "searchbox", clocator: [title: "Google Search"])
    SubmitButton(uid: "googlesearch", clocator: [name: "btnG", value: "Google Search"])
    SubmitButton(uid: "Imfeelinglucky", clocator: [value: "I'm Feeling Lucky"])
}

//define Tellurium project menu
ui.Container(uid: "menu", clocator: [tag: "table", id: "mt", trailer: "/tbody/tr/th"], group: "true"){
    UrlLink(uid: "project_home", clocator: [text: "%%Home"])
    UrlLink(uid: "downloads", clocator: [text: "Downloads"])
    UrlLink(uid: "wiki", clocator: [text: "Wiki"])
    UrlLink(uid: "issues", clocator: [text: "Issues"])
    UrlLink(uid: "source", clocator: [text: "Source"])
}

//define the Tellurium project search module, which includes an input box, two search buttons
ui.Form(uid: "search", clocator: [:], group: "true"){
    InputBox(uid: "searchbox", clocator: [name: "q"])
    SubmitButton(uid: "search_project_button", clocator: [value: "Search Projects"])
    SubmitButton(uid: "search_web_button", clocator: [value: "Search the Web"])
}

openUrl "http://www.google.com"
type "google_start_page.searchbox", "Tellurium Selenium"
pause 500
click "google_start_page.SubmitButton"
waitForPageToLoad 30000

openUrl "http://code.google.com/p/aost/"
click "menu.project_home"
waitForPageToLoad 30000
click "menu.downloads"
waitForPageToLoad 30000
click "menu.wiki"
waitForPageToLoad 30000
click "menu.issues"
waitForPageToLoad 30000

openUrl "http://code.google.com/p/aost/"
type "search.searchbox", "Tellurium Selenium"
click "search.search_project_button"
waitForPageToLoad 30000

type "search.searchbox", "tellurium selenium dsl groovy"
click "search.search_web_button"
waitForPageToLoad 30000
}}}

In your own .dsl scripts you can put assertions in. 

To run the .dsl file, you can call "DslScriptExecutor dsl_file". For example, in the project directory, you can run google.dsl as follows:

{{{
java -cp ./lib/junit-3.8.2.jar:./lib/selenium-java-client-driver.jar:./lib/selenium-server.jar:./lib/groovy-all-1.5.6.jar:./dist/tellurium-0.4.0.jar aost.dsl.DslScriptExecutor src/example/dsl/google.dsl 
}}}

Here we assume you have created the tellurium-0.4.0.jar in the "dist/" directory by running 
{{{
ant compile-test
}}}
 
There is a shell script "rundsl.sh" in the project directory and you can simply run

{{{
./rundsl.sh src/example/dsl/google.dsl 
}}}

In windows, please run rundsl.bat instead.


=== Google Book List and Code Hosting pages ===

The example described in the Data driven testing session, i.e., Google Book List and Google Code Hosting pages, can also be written in pure DSL as follows,

{{{
//define UI module
ui.Container(uid: "GoogleBooksList", clocator: [tag: "table", id: "hp_table"], group: "true") {
    TextBox(uid: "category", clocator: [tag: "div", class: "sub_cat_title"])
    List(uid: "subcategory", clocator: [tag: "div", class: "sub_cat_section"], separator: "/p") {
      UrlLink(uid: "all", locator: "/a")
    }
}

ui.Table(uid: "labels_table", clocator: [:], group: "true"){
    TextBox(uid: "row: 1, column: 1", clocator: [tag: "div", text: "Example project labels:"])
    Table(uid: "row: 2, column: 1", clocator: [header: "/div[@id=\"popular\"]"]){
        UrlLink(uid: "all", locator: "/a")
    }
}

//define file data format
fs.FieldSet(name: "GoogleBookList", description: "Google Book List") {
    Test(value: "checkBookList")
    Field(name: "category", description: "book category")
    Field(name: "size", type: "int", description: "google book list size ")
}

fs.FieldSet(name: "GCHStatus", description: "Google Code Hosting input") {
    Test(value: "getGCHStatus")
    Field(name: "label")
    Field(name: "rowNum", type: "int")
    Field(name: "columNum", type: "int")
}

fs.FieldSet(name: "GCHLabel", description: "Google Code Hosting input") {
    Test(value: "clickGCHLabel")
    Field(name: "row", type: "int")
    Field(name: "column", type: "int")
}

defineTest("getGCHStatus"){
    def expectedLabel = bind("GCHStatus.label")
    def expectedRowNum = bind("GCHStatus.rowNum")
    def expectedColumnNum = bind("GCHStatus.columNum")

    openUrl("http://code.google.com/hosting/")
    def label = getText("labels_table[1][1]")
    def rownum = getTableMaxRowNum("labels_table[2][1]")
    def columnum = getTableMaxColumnNum("labels_table[2][1]")

    compareResult(expectedLabel, label)
    compareResult(expectedRowNum, rownum)
    compareResult(expectedColumnNum, columnum)
    pause 1000
}

defineTest("clickGCHLabel"){
    def row = bind("GCHLabel.row")
    def column = bind("GCHLabel.column")

    openUrl("http://code.google.com/hosting/")
    click  "labels_table[2][1].[${row}][${column}]"

    waitForPageToLoad 30000
}

defineTest("checkBookList"){
    def expectedCategory = bind("GoogleBookList.category")
    def expectedSize = bind("GoogleBookList.size")
    openUrl("http://books.google.com/")
    String category = getText("GoogleBooksList.category")
    compareResult(expectedCategory, category)

    int size = getListSize("GoogleBooksList.subcategory")
    compareResult(expectedSize, size){
        assertTrue(expectedSize == size)
    }
}

//load file
loadData "src/test/example/test/ddt/GoogleBookListCodeHostInput.txt"

//read each line and run the test script until the end of the file
stepToEnd()

/close file
closeData()

}}}